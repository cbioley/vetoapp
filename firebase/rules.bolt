//https://github.com/firebase/bolt/blob/master/docs/language.md

// Functions

isAdmin() { root.roles.admins[auth.uid] != null }
isServerQueue() { root.roles.queues[auth.uid] != null }
isSignedIn() { auth != null }
isViewer(uid) { isSignedIn() && auth.uid == uid }
userExits(uid) { root.users[uid] != null }
vetoExits(id) { root.vetos[id] != null }

// Types

type ShortString extends String {
  validate() { this.length <= 100 }
}

type ShortRequiredString extends String {
  // Required form field with maxLength="100".
  validate() { this.length > 0 && this.length <= 100 }
}

type LongString extends String {
  validate() { this.length <= 1000 }
}

type LongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 1000 }
}

type ExtraLongString extends String {
  validate() { this.length <= 10000 }
}

type ExtraLongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 10000 }
}

// https://github.com/firebase/firebase-queue#queue-security
type FirebaseQueueStateErrorState {
  error: String,
  error_stack: String,
  previous_state: String,
  original_task: Any | Null,
  attempts: Number
}

// https://github.com/firebase/firebase-queue#queue-security
type FirebaseQueueState {
  _state: String,
  _state_changed: Number,
  _owner: String,
  _progress: Number,
  _error_details: FirebaseQueueStateErrorState
}

type User {
  authenticatedAt: Number,
  displayName: LongString,
  id: ShortRequiredString,
  profileImageURL: LongString,
  provider: LongString
}

type Email {
  email: ShortRequiredString
}

type Veto {
  country: ShortRequiredString,
  createdAt: Number,
  creatorDisplayName: ShortRequiredString,
  creatorId: ShortRequiredString,
  creatorProfileImageURL: LongString,
  id: ShortRequiredString,
  municipality: ShortString,
  name: LongRequiredString,
  reason: ExtraLongRequiredString,
  updatedAt: Number

  validate() { isAdmin() || isViewer(this.creatorId) }
}

type Vote {
  createdAt: Number,
  userId: ShortRequiredString,
  vetoId: ShortRequiredString
  yes: Boolean

  validate() {
    userExits(this.userId) && vetoExits(this.vetoId)
  }
}

// Paths.

path /roles/admins/{uid} {
  read() { isViewer(uid) }
}

path /users {
  index() { ["authenticatedAt"] }
  read() { true }

  path /{uid} is User {
    write() { isViewer(uid) }
  }
}

path /users-emails/{uid} is Email {
  read() { isViewer(uid) }
  write() { isViewer(uid) }
}

path /vetos {
  index() { ["createdAt", "creatorId"] }
  read() { true }

  path /{id} is Veto {
    // https://www.firebase.com/blog/2016-01-26-easier-write-rules-bolt.html
    create() { this.id == id }
    delete() { isAdmin() || isViewer(prior(this).creatorId) }
    update() { this.id == id && (isAdmin() || isViewer(prior(this).creatorId)) }
  }
}

path /vetos-archived/{id} is Veto {
  create() { this.id == id }
  delete() { isViewer(prior(this).creatorId) }
}

path /vetos-votes-queue {
  path /tasks {
    index() { ["_state"] }
    read() { isServerQueue() }

    path /{id} is Vote | FirebaseQueueState {
      write() { isViewer(this.userId) || isServerQueue() }
    }
  }
  // TODO: Add specs.
}

path /vetos-votes-yes/{id} is Vote {
  index() { ["userId", "vetoId"] }
  read() { true }
  write() { isServerQueue() }
}

path /vetos-votes-no/{id} is Vote {
  index() { ["userId", "vetoId"] }
  read() { true }
  write() { isServerQueue() }
}

path /vetos-votes-yes-total/{vetoId} is Number {
  read() { true }
  write() { isServerQueue() }
}
