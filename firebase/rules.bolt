// https://github.com/firebase/bolt/blob/master/docs/language.md

// Functions

isAdmin() { root.roles.admins[auth.uid] != null }
isQueue() { root.roles.queues[auth.uid] != null }
isSignedIn() { auth != null }
isViewer(uid) { isSignedIn() && uid == auth.uid }
isViewerVeto(vetoId) { root.vetos[vetoId].creatorId == auth.uid }
userExits(uid) { root.users[uid] != null }
vetoExits(id) { root.vetos[id] != null }

// Types

type ShortString extends String {
  validate() { this.length <= 100 }
}

type ShortRequiredString extends String {
  // Required form field with maxLength="100".
  validate() { this.length > 0 && this.length <= 100 }
}

type LongString extends String {
  validate() { this.length <= 1000 }
}

type LongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 1000 }
}

type ExtraLongString extends String {
  validate() { this.length <= 10000 }
}

type ExtraLongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 10000 }
}

// https://github.com/firebase/firebase-queue#queue-security
type FirebaseQueueStateErrorDetails {
  attempts: Number,
  error: String,
  error_stack: String,
  original_task: Any | Null,
  previous_state: String
}

// https://github.com/firebase/firebase-queue#queue-security
type FirebaseQueueState {
  _error_details: FirebaseQueueStateErrorDetails,
  _id: String,
  _owner: String,
  _progress: Number,
  _state: String,
  _state_changed: Number
}

type User {
  authenticatedAt: Number,
  displayName: LongRequiredString,
  id: ShortRequiredString,
  profileImageURL: LongString,
  provider: ShortRequiredString
}

type Email {
  email: ShortRequiredString
}

type Veto {
  country: ShortRequiredString,
  createdAt: Number,
  creatorDisplayName: ShortRequiredString,
  creatorId: ShortRequiredString,
  creatorProfileImageURL: LongString,
  id: ShortRequiredString,
  municipality: ShortString,
  name: LongRequiredString,
  reason: ExtraLongRequiredString,
  updatedAt: Number
}

type VetoArchiveQueueItem {
  vetoId: ShortRequiredString

  validate() {
    vetoExits(this.vetoId)
  }
}

type Vote {
  createdAt: Number,
  userDisplayName: ShortRequiredString,
  userId: ShortRequiredString,
  userProfileImageURL: LongString,
  vetoCountry: ShortRequiredString,
  vetoCreatorDisplayName: ShortRequiredString,
  vetoCreatorId: ShortRequiredString,
  vetoId: ShortRequiredString,
  vetoMunicipality: ShortString,
  vetoName: LongRequiredString,
  yes: Boolean

  validate() {
    userExits(this.userId) && vetoExits(this.vetoId)
  }
}

type VoteQueueItem {
  userId: ShortRequiredString,
  vetoId: ShortRequiredString,
  yes: Boolean

  validate() {
    userExits(this.userId) && vetoExits(this.vetoId)
  }
}

type VoteTotal {
  total: Number,
  updatedAt: Number,
  vetoCountry: ShortRequiredString,
  vetoCreatorDisplayName: ShortRequiredString,
  vetoCreatorId: ShortRequiredString,
  vetoId: ShortRequiredString,
  vetoMunicipality: ShortString,
  vetoName: LongRequiredString

  validate() {
    userExits(this.vetoCreatorId) && vetoExits(this.vetoId)
  }
}

// Paths.

path /roles/admins/{uid} {
  read() { isViewer(uid) }
}

path /users {
  index() { ["authenticatedAt"] }
  read() { true }

  path /{uid} is User {
    write() { isViewer(uid) }
  }
}

path /users-emails/{uid} is Email {
  read() { isViewer(uid) }
  write() { isViewer(uid) }
}

path /vetos {
  index() { ["createdAt", "creatorId"] }
  read() { true }

  path /{id} is Veto {
    create() { id == this.id && isViewer(this.creatorId) }
    delete() { isQueue() || isAdmin() || isViewer(prior(this).creatorId) }
    update() { isQueue() || isAdmin() || isViewer(prior(this).creatorId) }
  }
}

path /vetos-archived/{id} is Veto {
  write() { isQueue() }
}

path /vetos-archive-queue {
  path /tasks {
    index() { ["_state"] }
    read() { isQueue() }

    path /{id} is VetoArchiveQueueItem | FirebaseQueueState {
      // For some reason, this works, while plain write doesn't work.
      // write() { isViewerVeto(this.vetoId) || isQueue() }
      create() { isViewerVeto(this.vetoId) }
      update() { isQueue() }
      delete() { isQueue() }
    }
  }
}

path /vetos-votes-queue {
  path /tasks {
    index() { ["_state"] }
    read() { isQueue() }

    path /{id} is VoteQueueItem | FirebaseQueueState {
      create() { isViewer(this.userId) }
      update() { isQueue() }
      delete() { isQueue() }
    }
  }
}

path /vetos-votes {
  index() { ["userId", "vetoId"] }
  read() { true }
  write() { isQueue() }

  path /{yesOrNo}/{vetosOrVotes}/{vetoIdOrUserId} {
    index() { ["createdAt"] }
    path /{userIdOrVetoId} is Vote {}
  }
}

path /vetos-votes-yes-total {
  read() { true }
  write() { isQueue() }

  path /{vetoCountryOrAll} {
    index() { ["total"] }
    path /{vetoId} is VoteTotal {}
  }
}
