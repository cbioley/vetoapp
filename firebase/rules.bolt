// An example of Firebase security and modeling language.
// https://github.com/firebase/bolt/blob/master/docs/guide.md

// Functions

isSignedIn() { auth != null }
isSignedInUser(uid) { isSignedIn() && auth.uid == uid }

// Types

type ShortString extends String {
  validate() { this.length <= 100 }
}

type ShortRequiredString extends String {
  // Required form field with maxLength="100".
  validate() { this.length > 0 && this.length <= 100 }
}

type LongString extends String {
  validate() { this.length <= 1000 }
}
type LongRequiredString extends String {
  validate() { this.length > 0 && this.length <= 1000 }
}

type User {
  authenticatedAt: Number,
  displayName: LongString,
  id: ShortRequiredString,
  profileImageURL: LongString,
  provider: LongString
}

type Email {
  email: ShortRequiredString
}

type Veto {
  country: ShortRequiredString,
  createdAt: Number,
  creatorDisplayName: ShortRequiredString,
  creatorId: ShortRequiredString,
  creatorProfileImageURL: LongString,
  id: ShortRequiredString,
  municipality: ShortString,
  name: LongRequiredString,
  reason: LongRequiredString,
  updatedAt: Number
}

// Paths

path /users {
  index() { ["authenticatedAt"] }
  read() { true }
}

path /users/{uid} is User {
  write() { isSignedInUser(uid) }
}

path /users-emails/{uid} is Email {
  read() { isSignedInUser(uid) }
  write() { isSignedInUser(uid) }
}

path /vetos {
  index() { ["country", "createdAt", "creatorDisplayName", "name"] }
  read() { true }
}

path /vetos/{id} is Veto {
  write() { isSignedInUser(this.creatorId) }
}
